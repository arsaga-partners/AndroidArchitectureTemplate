class Version {
    static final code = 1
    static final name = "1.0.0"
}

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

    //コピペを簡便にするためのプロジェクトテンプレートにあるライブラリ専用のバージョン置き場
    ext.kotlin_version = "1.5.21"
    ext.coroutine_version = "1.5.1"
    ext.hilt_version = "2.37"
    ext.nav_version = "2.3.5"
    ext.lifecycle_version = "2.3.1"
    ext.timber_version = "4.7.1"
    ext.appcompat_version = "1.3.0"
    ext.material_version = "1.4.0"
    ext.constraintlayout_version = "2.0.4"
    ext.security_version = "1.0.0"

    repositories {
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath 'com.google.gms:google-services:4.3.8'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.7.1'
    }
}

subprojects {
    afterEvaluate { subProject ->
        if (subProject.hasProperty('android')) {
            dependencies {

                implementation "com.jakewharton.timber:timber:$timber_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

                switch (subProject.getPath()) {
                    case ":app":

                        break

                    case ":presentation:view:core":
                        api "androidx.appcompat:appcompat:$appcompat_version"
                        api "com.google.android.material:material:$material_version"
                        api "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
                        api "androidx.navigation:navigation-fragment-ktx:$nav_version"
                        api "androidx.navigation:navigation-ui-ktx:$nav_version"
                        break

                    case ":presentation:viewModel:core":
                        api "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
                        api "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
                        break

                    case ":dataStore:repository:core":
                        implementation "androidx.security:security-crypto:$security_version"
                        break

                    case ":domain:service:core":
                        api "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
                        break
                }
            }

            android {
                def version = new Version()
                compileSdkVersion 30
                defaultConfig {
                    versionCode = version.code
                    versionName = version.name
                    targetSdkVersion 30
                    buildConfigField("String", "APP_VERSION", "\"" + versionName + "\"")
                }
                kotlinOptions {
                    jvmTarget = '1.8'
                }
                buildFeatures {
                    dataBinding true
                    viewBinding true
                }
            }
        }
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}